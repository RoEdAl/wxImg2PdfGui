#
# InnoSetup.cmake
#
FOREACH(H ${CMAKE_SYSTEM_PREFIX_PATH})
	LIST(APPEND ISCCHINT "${H}/Inno Setup 6")
ENDFOREACH()
FIND_PROGRAM(ISCC "iscc" HINT ${ISCCHINT} NO_CACHE)
IF(NOT ISCC)
	MESSAGE(FATAL_ERROR "InnoSetup not found")
ENDIF()

#CMAKE_PATH(APPEND CPACK_TOPLEVEL_DIRECTORY ${CPACK_PACKAGE_FILE_NAME}.json OUTPUT_VARIABLE JSON_DESC_FILE)
CMAKE_PATH(APPEND CPACK_TOPLEVEL_DIRECTORY ${CPACK_PACKAGE_FILE_NAME} pkg-desc.json OUTPUT_VARIABLE JSON_DESC_FILE)
IF(NOT EXISTS ${JSON_DESC_FILE})
	MESSAGE(FATAL_ERROR "Unable to find ${JSON_DESC_FILE}")
ENDIF()

FILE(READ "${JSON_DESC_FILE}" JSON_DESC)
IF(NOT JSON_DESC)
	MESSAGE(FATAL_ERROR "Unable to read JSON file: ${JSON_DESC_FILE}")
ENDIF()

STRING(JSON CFG_BUILD_CONFIG GET ${JSON_DESC} buildConfig)
IF(NOT CFG_BUILD_CONFIG)
	MESSAGE(FATAL_ERROR "Unknown build config")
ENDIF()
MESSAGE(VERBOSE "Build config: ${CFG_BUILD_CONFIG}")

STRING(JSON CFG_COMPONENT_NAME GET ${JSON_DESC} componentName)
STRING(JSON CFG_PACKAGE_NAME GET ${JSON_DESC} packageName)
MESSAGE(VERBOSE "Component name: ${CFG_COMPONENT_NAME}")
MESSAGE(VERBOSE "Package name: ${CFG_PACKAGE_NAME}")

SET(SETUP_FILES_DIR ${CPACK_TEMPORARY_DIRECTORY}/${CFG_COMPONENT_NAME})
IF(NOT EXISTS ${SETUP_FILES_DIR})
	MESSAGE(FATAL_ERROR "Wrong setup files directory: ${SETUP_FILES_DIR}")
ENDIF()

SET(MAIN_EXE_PATH ${SETUP_FILES_DIR}/${CFG_PACKAGE_NAME}.exe)
IF(DEFINED ENV{SOURCE_DATE_EPOCH})
	STRING(TIMESTAMP TOUCH_DATE "%Y-%m-%d" UTC)
	STRING(TIMESTAMP TOUCH_TIME "%H:%M" UTC)
ELSE()
	FILE(TIMESTAMP ${MAIN_EXE_PATH} TOUCH_DATE "%Y-%m-%d" UTC)
	FILE(TIMESTAMP ${MAIN_EXE_PATH} TOUCH_TIME "%H:%M" UTC)
ENDIF()

SET(ISETUP_SCRIPT ${CMAKE_CURRENT_LIST_DIR}/setup.iss)

UNSET(ISETUP_ARGS)
IF(${CFG_BUILD_CONFIG} STREQUAL Debug)
	SET(SETUP_FN ${CPACK_PACKAGE_FILE_NAME}-dbg)
ELSE()
	SET(SETUP_FN ${CPACK_PACKAGE_FILE_NAME})
	LIST(APPEND ISETUP_ARGS "/Q")
ENDIF()

MESSAGE(VERBOSE "Working directory: ${SETUP_FILES_DIR}")
MESSAGE(VERBOSE "Output: ${CPACK_TOPLEVEL_DIRECTORY}/${SETUP_FN}.exe")
MESSAGE(VERBOSE "Script: ${ISETUP_SCRIPT}")
MESSAGE(STATUS "InnoSetup: create ${SETUP_FN}.exe [${TOUCH_DATE} ${TOUCH_TIME}: ${CFG_BUILD_CONFIG}]")

EXECUTE_PROCESS( 
	COMMAND ${ISCC}
		/o${CPACK_TOPLEVEL_DIRECTORY}
		/f${SETUP_FN}
		/dImg2PdfFilesDir=${SETUP_FILES_DIR}
		/dImg2PdfBase=${CFG_PACKAGE_NAME}
		/dImg2PdfExe=${MAIN_EXE_PATH}
		/dImg2PdfTouchDate=${TOUCH_DATE}
		/dImg2PdfTouchTime=${TOUCH_TIME}		
		/dSetupIconFile=${CMAKE_CURRENT_LIST_DIR}/../gui/ico/picture-as-pdf.ico
		/dImg2PdfArch=x64
		/dMuPdfVersion=1.20.0
		/dMuPdfChecksum=a847eb233fcbbbb8b8fe1425bc57c3701542036b71dc32188d622fba94b0d4e0
		/dSumatraPdfVersion=3.4.6
		/dSumatraPdfChecksum=2bb05aa8b74bc748bc1f6a2b6f6ec4ba22bd5b1eaeec767d0a7f97cfd436d40d
		${ISETUP_ARGS}
		${ISETUP_SCRIPT}
	ENCODING UTF-8
	COMMAND_ERROR_IS_FATAL ANY
	WORKING_DIRECTORY ${CPACK_TEMPORARY_DIRECTORY}
)

SET(CPACK_EXTERNAL_BUILT_PACKAGES ${CPACK_TOPLEVEL_DIRECTORY}/${SETUP_FN}.exe)
