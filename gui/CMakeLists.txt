#
# img2pdfGUI
#

FILE(GLOB IMG2PDF_GUI_HEADER_FILES "*.h")
FILE(GLOB IMG2PDF_GUI_ICON_FILES "*.ico" )
GET_FILENAME_COMPONENT(IMG2PDF_GUI_DIR_NAME ${CMAKE_CURRENT_SOURCE_DIR} NAME)

SOURCE_GROUP("Resource Files" FILES Img2PdfGui.rc ${IMG2PDF_GUI_ICON_FILES})

SET(IMG2PDF_GUI_SOURCE_FILES
	wxApp.cpp
	wxMainFrame.cpp
	LogListBox.cpp
	VariantExt.cpp
)

ADD_EXECUTABLE(wxImg2PdfGui WIN32
	${IMG2PDF_GUI_SOURCE_FILES}
	${IMG2PDF_GUI_HEADER_FILES}
	Img2PdfGui.rc
	${IMG2PDF_GUI_ICON_FILES}
	${PROJECT_CONFIG_FILE}
)

ConfigureWxApp(wxImg2PdfGui)
ConfigureWxWidgetsUsage(wxImg2PdfGui)
ConfigureSourceEncoding(wxImg2PdfGui)
SET_PROPERTY(TARGET wxImg2PdfGui PROPERTY OUTPUT_NAME wximg2pdf)
TARGET_LINK_LIBRARIES(wxImg2PdfGui
	PRIVATE wxCmdTools wxEncodingDetection nlohmann_json::nlohmann_json
)

SET(MANIFEST_DEFS wxUSE_RC_MANIFEST=1 wxUSE_DPI_AWARE_MANIFEST=2)
SET_SOURCE_FILES_PROPERTIES(Img2PdfGui.rc PROPERTIES COMPILE_DEFINITIONS "${MANIFEST_DEFS}")
ADD_CUSTOM_COMMAND(TARGET wxImg2PdfGui POST_BUILD
	COMMAND ${CMAKE_COMMAND} -E copy_if_different $<TARGET_RUNTIME_DLLS:wxImg2PdfGui> $<TARGET_FILE_DIR:wxImg2PdfGui>
	COMMAND_EXPAND_LISTS
)

IF(DEFINED MINGW_CFG_FILE)
	ADD_CUSTOM_COMMAND(TARGET wxImg2PdfGui POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E copy_if_different $<TARGET_PROPERTY:GNU_gcc,IMPORTED_LOCATION> $<TARGET_PROPERTY:GNU_stdcxx,IMPORTED_LOCATION> $<TARGET_FILE_DIR:wxImg2PdfGui>
	)
ENDIF()

ADD_DEPENDENCIES(wxImg2PdfGui license img2pdf-js)

INSTALL(
	TARGETS wxImg2PdfGui
	DESTINATION img2pdf
	COMPONENT img2pdf
)
