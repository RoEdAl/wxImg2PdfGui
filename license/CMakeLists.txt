#
# License (Pandoc)
#

FIND_PROGRAM(PANDOC_BIN "pandoc")
IF(NOT PANDOC_BIN)
	MESSAGE(FATAL_ERROR "Pandoc utility not found")
ENDIF()
ADD_EXECUTABLE(Pandoc IMPORTED)
SET_PROPERTY(TARGET Pandoc PROPERTY IMPORTED_LOCATION ${PANDOC_BIN})

SET(LICENSE_FILE license.md.txt)
SET(LICENSE_FILE_TEMPLATE template.rtf)


ADD_CUSTOM_COMMAND( 
	OUTPUT $<CONFIG>/license.rtf
	MAIN_DEPENDENCY ${LICENSE_FILE}
	DEPENDS ${LICENSE_FILE_TEMPLATE}
	COMMAND Pandoc
		-i ${LICENSE_FILE}
		--template ${LICENSE_FILE_TEMPLATE}
		-s
		-t rtf
		-M lang:en-US
		-o ${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/license.rtf
	COMMENT "Creating license file [RTF]"
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

ADD_CUSTOM_COMMAND( 
	OUTPUT $<CONFIG>/license.txt
	MAIN_DEPENDENCY ${LICENSE_FILE}
	COMMAND Pandoc
		-i ${LICENSE_FILE}
		-s
		-t plain+smart
		-M lang:en-US
		-o ${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/license.txt
	COMMENT "Creating license file [TXT]"
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

ADD_CUSTOM_TARGET(license ALL
	SOURCES ${LICENSE_FILE} ${LICENSE_FILE_TEMPLATE}
	DEPENDS
		${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/license.txt
		${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/license.rtf
)
TARGET_SOURCES(license PRIVATE ${LICENSE_FILE} ${LICENSE_FILE_TEMPLATE})

INSTALL(
	FILES
		${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/license.rtf
		${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/license.txt
	DESTINATION img2pdf
	COMPONENT img2pdf
	EXCLUDE_FROM_ALL
)
